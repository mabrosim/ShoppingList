apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        applicationId "fi.mabrosim.shoppinglist"

        versionCode 10
        versionName "18w07.5.1"
    }
    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
}

android.applicationVariants.all { variant ->
    variant.resValue "string", "versionName", variant.versionName
}

dependencies {
    implementation project(':protobuf')
    implementation 'com.android.support:appcompat-v7:' + rootProject.ext.supportLibVersion
    implementation 'com.android.support:design:' + rootProject.ext.supportLibVersion
    implementation 'com.android.support:support-v4:' + rootProject.ext.supportLibVersion
    implementation 'com.github.satyan:sugar:1.5'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation('com.opencsv:opencsv:4.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }
    testImplementation 'junit:junit:' + rootProject.ext.junitVersion
    testImplementation "org.robolectric:robolectric:3.7"
}
